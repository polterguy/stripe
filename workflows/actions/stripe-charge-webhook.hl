
/*
 * Charge POST webhook helper action, allowing you to embed into a Stripe webhook,
 * to verify a charge, and extract its most important information.
 *
 * DO NOT change the default expressions that already have values since the validation
 * logic depends upon these retrieving these from the [.arguments] collection that's
 * provided by Stripe as it invokes our webhook.
 *
 * Notice, the [type] returned from this action will have one of these values
 *
 * - charge.captured
 * - charge.expired
 * - charge.failed
 * - charge.pending
 * - charge.refunded
 * - charge.succeeded
 * - charge.updated
 * - charge.dispute.closed
 * - charge.dispute.created
 * - charge.dispute.funds_reinstated
 * - charge.dispute.funds_withdrawn
 * - charge.dispute.updated
 * - charge.refund.updated
 */
.arguments
   customer_id
      type:string
      mandatory:bool:true
      default:":x:@.arguments/*/data/*/object/*/customer"
   email
      type:string
      mandatory:bool:true
      default:":x:@.arguments/*/data/*/object/*/billing_details/*/email"
   name
      type:string
      mandatory:bool:true
      default:":x:@.arguments/*/data/*/object/*/billing_details/*/name"
   amount
      type:string
      mandatory:bool:true
      default:":x:@.arguments/*/data/*/object/*/amount"
   currency
      type:string
      mandatory:bool:true
      default:":x:@.arguments/*/data/*/object/*/currency"
   receipt_url
      type:string
      mandatory:bool:true
      default:":x:@.arguments/*/data/*/object/*/receipt_url"
   type
      type:string
      mandatory:bool:true
      default:":x:@.arguments/*/type"
.icon:payment

// Sanity checking invocation.
validators.mandatory:x:@.arguments/*/customer_id
validators.mandatory:x:@.arguments/*/amount
validators.mandatory:x:@.arguments/*/currency
validators.mandatory:x:@.arguments/*/receipt_url
validators.mandatory:x:@.arguments/*/type

// Converting amount to decimal and dividing by 100.
math.divide
   convert:x:@.arguments/*/amount
      type:decimal
   .:decimal:100

// Returning result to caller.
yield
   customer_id:x:@.arguments/*/customer_id
   amount:x:@math.divide
   currency:x:@.arguments/*/currency
   receipt_url:x:@.arguments/*/receipt_url
   type:x:@.arguments/*/type
   username:x:@.arguments/*/username
