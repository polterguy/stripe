
/*
 * Charge webhook helper action, allowing you to embed into a Stripe webhook, verify a charge,
 * and extract its most important information.
 *
 * Requires payment/subscription to have been created using this module, since
 * it relies upon "metadata.secret" to be able to verify payment. The action will
 * return [valid] as true if the webhook invocation is legitimate according to
 * the metadata.secret argument, otherwise it will return false.
 *
 * DO NOT change the expressions for [customer_id], [secret] and [amount] since the validation
 * logic depends upon these retrieving these from the [.arguments] collection that's
 * provided by Stripe as it invokes our webhook.
 */
.arguments
   customer_id
      type:string
      mandatory:bool:true
      default:":x:@.arguments/*/data/*/object/*/customer"
   secret
      type:string
      mandatory:bool:true
      default:":x:@.arguments/*/data/*/object/*/metadata/*/secret"
   amount
      type:string
      mandatory:bool:true
      default:":x:@.arguments/*/data/*/object/*/amount"
   currency
      type:string
      mandatory:bool:true
      default:":x:@.arguments/*/data/*/object/*/currency"
   receipt_url
      type:string
      mandatory:bool:true
      default:":x:@.arguments/*/data/*/object/*/receipt_url"
.icon:payment

// Sanity checking invocation.
validators.mandatory:x:@.arguments/*/customer_id
validators.mandatory:x:@.arguments/*/secret
validators.mandatory:x:@.arguments/*/amount
validators.mandatory:x:@.arguments/*/currency
validators.mandatory:x:@.arguments/*/receipt_url

// Hashing customer_id + auth.secret to verify webhook invocation was legitimate.
strings.concat
   get-value:x:@.arguments/*/customer_id
   .:.
   config.get:"magic:auth:secret"

// Verifying secret argument.
if
   neq:x:@strings.value
      get-value:x:@.arguments/*/secret
   .lambda

      // NOT a valid webhook invocation.
      return
         valid:bool:false

// Converting amount to decimal and dividing by 100.
math.divide
   convert:x:@.arguments/*/amount
      type:decimal
   .:decimal:100

// Returning result to caller.
yield
   valid:bool:true
   customer_id:x:@.arguments/*/customer_id
   amount:x:@math.divide
   currency:x:@.arguments/*/currency
   receipt_url:x:@.arguments/*/receipt_url
