
/*
 * Charge POST webhook helper action, allowing you to embed into a Stripe webhook,
 * to extract its most important information.
 *
 * DO NOT change the default expression for the [node_reference] argument since these
 * are arguments forwarded by Stripe the action depends upon to function.
 *
 * Notice, the [type] returned from this action will have one of these values.
 *
 * - charge.captured
 * - charge.expired
 * - charge.failed
 * - charge.pending
 * - charge.refunded
 * - charge.succeeded
 * - charge.updated
 * - charge.dispute.closed
 * - charge.dispute.created
 * - charge.dispute.funds_reinstated
 * - charge.dispute.funds_withdrawn
 * - charge.dispute.updated
 * - charge.refund.updated
 */
.arguments
   node_reference
      type:string
      mandatory:bool:true
      default:":x:@.arguments"
.icon:payment

// Converting amount to decimal and dividing by 100.
math.divide
   convert:x:@.arguments/*/node_reference/#/*/data/*/object/*/amount
      type:decimal
   .:decimal:100

// Returning result to caller.
yield
   customer_id:x:@.arguments/*/node_reference/#/*/data/*/object/*/customer
   amount:x:@math.divide
   currency:x:@.arguments/*/node_reference/#/*/data/*/object/*/currency
   receipt_url:x:@.arguments/*/node_reference/#/*/data/*/object/*/receipt_url
   type:x:@.arguments/*/node_reference/#/*/type
   metadata:x:@.arguments/*/node_reference/#/*/data/*/object/*/metadata/*
