
/*
 * Charge POST webhook helper action, allowing you to embed into a Stripe webhook,
 * to verify a charge, and extract its most important information.
 *
 * Requires payment/subscription to have been created using this module, since
 * it relies upon "metadata.secret" to be able to verify payment. The action will
 * return [verified] as true if the webhook invocation is legitimate according to
 * the metadata.secret argument, otherwise it will return false.
 *
 * DO NOT change the default expressions that already have values since the validation
 * logic depends upon these retrieving these from the [.arguments] collection that's
 * provided by Stripe as it invokes our webhook.
 *
 * Notice, a charge might still be valid, even though the action return [verified] as
 * false, since the webhook will only have the [metadata]/[secret] argument if the
 * payment was created using one of the other actions in the Stripe module. If the payment
 * was for instance manually created, or created some other way, [verified] will be false,
 * but the payment might still be valid.
 */
.arguments
   customer_id
      type:string
      mandatory:bool:true
      default:":x:@.arguments/*/data/*/object/*/customer"
   username
      type:string
      mandatory:bool:true
      default:":x:@.arguments/*/data/*/object/*/metadata/*/username"
   secret
      type:string
      mandatory:bool:true
      default:":x:@.arguments/*/data/*/object/*/metadata/*/secret"
   email
      type:string
      mandatory:bool:true
      default:":x:@.arguments/*/data/*/object/*/billing_details/*/email"
   name
      type:string
      mandatory:bool:true
      default:":x:@.arguments/*/data/*/object/*/billing_details/*/name"
   amount
      type:string
      mandatory:bool:true
      default:":x:@.arguments/*/data/*/object/*/amount"
   currency
      type:string
      mandatory:bool:true
      default:":x:@.arguments/*/data/*/object/*/currency"
   receipt_url
      type:string
      mandatory:bool:true
      default:":x:@.arguments/*/data/*/object/*/receipt_url"
   type
      type:string
      mandatory:bool:true
      default:":x:@.arguments/*/type"
.icon:payment

// Sanity checking invocation.
validators.mandatory:x:@.arguments/*/customer_id
validators.mandatory:x:@.arguments/*/amount
validators.mandatory:x:@.arguments/*/currency
validators.mandatory:x:@.arguments/*/receipt_url

/*
 * Used to track if payment is verified or not.
 *
 * Notice, we can only verify payments that have the correct meta data, which this module automatically
 * takes care of as it is creating customers, but will NOT be the case for payment links, or payments
 * that were somehow created outside of this module.
 *
 * This implies you might have to manually verify a payment if it was created with something else than
 * through a "stripe-payment-create" action.
 *
 * And the verification process will ONLY work if it originated from the same server, and/or a server
 * that has the same "magic:auth:secret" value, since this is used as a secret during hashing.
 */
.verified:bool:false

// Hashing customer_id + auth.secret to verify webhook invocation was legitimate.
strings.concat
   get-value:x:@.arguments/*/customer_id
   .:.
   config.get:"magic:auth:secret"

// Verifying secret argument.
if
   eq
      crypto.hash.sha256:x:@strings.concat
      get-value:x:@.arguments/*/secret
   .lambda

      // Not a verified payment.
      set-value:x:@.verified
         .:bool:true

// Converting amount to decimal and dividing by 100.
math.divide
   convert:x:@.arguments/*/amount
      type:decimal
   .:decimal:100

// Returning result to caller.
yield
   verified:x:@.verified
   customer_id:x:@.arguments/*/customer_id
   amount:x:@math.divide
   currency:x:@.arguments/*/currency
   receipt_url:x:@.arguments/*/receipt_url
   type:x:@.arguments/*/type
   username:x:@.arguments/*/username
