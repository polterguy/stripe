
/*
 * Stripe integration allowing users to create a new payment method and associate it with their user.
 */
.arguments
   card_number:string
   card_exp_month:string
   card_exp_year:string
   card_cvs:string
   hidden:bool
.description:Creates a new payment method and associates with the currently authenticated user

// Sanity checking invocation.
auth.ticket.verify:root, admin, guest, impersonated

// Sanity checking invocation.
validators.mandatory:x:@.arguments/*/card_number
validators.mandatory:x:@.arguments/*/card_exp_month
validators.mandatory:x:@.arguments/*/card_exp_year
validators.mandatory:x:@.arguments/*/card_cvs

// Retrieving username that we'll need to create/extract customer from our internal systems.
auth.ticket.get

/*
 * Opening up a database connection to our stripe database, since we want to store
 * values associating the username with stripe internal data as we proceed.
 */
data.connect:[generic|stripe]

   // Will contain customer-id reference from Stripe's systems.
   .customer-id
   data.read
      table:customers
      values
         customer_id
      where
         and
            username.eq:x:@auth.ticket.get

   /*
    * Checking if we have previously create a customer object, and if not,
    * we create a new customer object.
    */
   if
      not
         exists:x:@data.read/*/*
      .lambda

         /*
          * Retrieving user's name and email address which we'll need to create our
          * customer in Stripe's system.
          */
         .email
         .name
         .ip
         data.connect:[generic|magic]
            data.read
               table:users_extra
               values
                  type
                  name
               where
                  and
                     user.eq:x:@auth.ticket.get
                     or
                        type.eq:email
                        type.eq:name
                        type.eq:registration-ip-address
            set-value:x:@.email
               get-value:x:@data.read/*/*/type/=email/./*/value
            set-value:x:@.name
               get-value:x:@data.read/*/*/type/=name/./*/value
            set-value:x:@.ip
               get-value:x:@data.read/*/*/type/=registration-ip-address/./*/value

         /*
          * Checking if we have an IP address associated with user,
          * and if not, trying to figure out the IP address of the current
          * request, and setting it.
          */
         if
            null:x:@.ip
            .lambda
               set-value:x:@.ip
                  get-first-value
                     request.headers.get:X-Real-IP
                     request.ip

         // We need to create a customer object.
         unwrap:x:+/*
         signal:stripe.customers.create
            username:x:@auth.ticket.get
            name:x:@.name
            email:x:@.email
            ip_address:x:@.ip

         // Inserting our customer object
         data.create
            table:customers
            values
               username:x:@auth.ticket.get
               customer_id:x:@signal

         // Setting customer id.
         set-value:x:@.customer-id
            get-value:x:@signal

   else

      // We already have a customer, storing for logic further down.
      set-value:x:@.customer-id
         get-value:x:@data.read/*/*/customer_id

   // Creating a new payment-method in Stripe's systems and storing a reference to it.
   .payment-method
   .brand
   unwrap:x:+/*
   signal:stripe.payment_methods.create
      card_number:x:@.arguments/*/card_number
      card_exp_month:x:@.arguments/*/card_exp_month
      card_exp_year:x:@.arguments/*/card_exp_year
      card_cvs:x:@.arguments/*/card_cvs

   /*
    * Making sure we can store the payment method and brand later and
    * associate with our internal records.
    */
   set-value:x:@.payment-method
      get-value:x:@signal/*/id
   set-value:x:@.brand
      get-value:x:@signal/*/brand

   // Attaching payment method with customer in Stripe.
   unwrap:x:+/*
   signal:stripe.payment_methods.attach
      payment_method:x:@.payment-method
      customer_id:x:@.customer-id

   // Creating our internal database record.
   strings.length:x:@.arguments/*/card_number
   math.subtract:x:-
      .:int:4
   strings.substring:x:@.arguments/*/card_number
      get-value:x:@math.subtract
      .:int:4
   data.create
      table:payment_methods
      values
         payment_method:x:@.payment-method
         username:x:@auth.ticket.get
         card_no:x:@strings.substring
         card_type:x:@.brand
         hidden:x:@.arguments/*/hidden

   // Checking if we've got a notifications callback, and if so, invoking it.
   config.get:"magic:stripe:notifications"
   unwrap:x:+/*/username
   try-signal:x:@config.get
      type:payments
      content:A payment method was associated with your account
      username:x:@auth.ticket.get

   // Returning 4 last digits of card, brand of card, and payment method ID to caller.
   unwrap:x:+/*
   return
      payment-method:x:@.payment-method
      brand:x:@.brand
      card_no:x:@strings.substring
