
/*
 * Webhook endpoint that you can configure in your Stripe dashboard to
 * receive notifications from Stripe as they are published.
 */
.arguments:*

// Making sure we only handle relevant events.
switch:x:@.arguments/*/type

   // Charge succeeded and failed. Storing type allowing us to later calculate amount.
   case:charge.failed
   case:charge.succeeded

      // Buffer object for transaction entity.
      .transaction
         transaction:x:@.arguments/*/id
         amount:x:@.arguments/*/data/*/object/*/amount_captured
         invoice:x:@.arguments/*/data/*/object/*/invoice
         username
         type:x:@.arguments/*/type
         payment_method:x:@.arguments/*/data/*/object/*/payment_method
      unwrap:x:-/*

      // Figuring out username transaction belongs to.
      data.connect:stripe
         data.read
            table:customers
            columns
               username
            where
               and
                  customer_id.eq:x:@.arguments/*/data/*/object/*/customer
         set-value:x:@.transaction/*/username
            get-value:x:@data.read/*/*/username

         // Creating our transaction object
         data.create
            table:transactions
            values
               transaction:x:@.transaction/*/transaction
               amount:x:@.transaction/*/amount
               invoice:x:@.transaction/*/invoice
               username:x:@.transaction/*/username
               type:x:@.arguments/*/type
               payment_method:x:@.transaction/*/payment_method
