
/*
 * Slot responsible for creating a subscription.
 *
 * Requires [price], [customer_id] and [payment_method] arguments.
 * Optionally pass in [meta] data as a string.
 */
slots.create:stripe.subscriptions.create

   // Sanity checking invocation.
   validators.mandatory:x:@.arguments/*/price
   validators.mandatory:x:@.arguments/*/customer_id
   validators.mandatory:x:@.arguments/*/payment_method

   // Retrieving token to use for invocations towards Stripe.
   .token
   set-value:x:-
      strings.concat
         .:"Bearer "
         config.get:"magic:stripe:token"

   // Checking if [meta] was supplied, and if so, associating it with request.
   if
      exists:x:@.arguments/*/meta
      .lambda
         unwrap:x:+/*/*
         add:x:../*/http.post/*/payload
            .
               metadata[meta]:x:@.arguments/*/meta

   // Invoking Stripe REST API.
   http.post:"https://api.stripe.com/v1/subscriptions"
      headers
         Content-Type:application/x-www-form-urlencoded
         Authorization:x:@.token
      payload
         customer:x:@.arguments/*/customer_id
         items[0][price]:x:@.arguments/*/price
         default_payment_method:x:@.arguments/*/payment_method
         automatic_tax[enabled]:bool:true
      convert:true

   // Sanity checking above invocation.
   if
      mte
         get-value:x:@http.post
         .:int:400
      .lambda

         // Oops, something went wrong ...!!
         lambda2hyper:x:@http.post
         log.error:Something went wrong while invoking Stripe API
            result:x:@lambda2hyper
            provider:Stripe
         throw:We could not create your subscription
            public:true
            status:500

   /*
    * Checking if charge was successful.
    * Notice, Stripe does NOT return an "authentication URL" for subscriptions,
    * for reasons I don't know, so the best we can do here, is to return finished
    * equaling false, and just encourage user to pay the invoice he have been
    * given on email.
    */
   if
      eq:x:@http.post/*/content/*/status
         .:incomplete
      .lambda

         // Charge was NOT successful, and probably needs 3D secure
         unwrap:x:+/*
         return
            finished:bool:false
            id:x:@http.post/*/content/*/id
            product:x:@http.post/*/content/*/items/*/data/0/*/price/*/product

   // Returning customer ID to caller.
   unwrap:x:+/*
   return
      finished:bool:true
      id:x:@http.post/*/content/*/id
      product:x:@http.post/*/content/*/items/*/data/0/*/price/*/product
