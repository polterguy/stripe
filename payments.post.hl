
/*
 * Stripe integration allowing users to purchase a product paying once immediately.
 */
.arguments
   amount:int
   currency:string
   payment_method:string
   meta:string
.description:Creates a new payment and associates with the currently authenticated user

// Sanity checking invocation.
auth.ticket.verify:root, admin, guest

// Sanity checking invocation.
validators.mandatory:x:@.arguments/*/amount
validators.mandatory:x:@.arguments/*/currency
validators.mandatory:x:@.arguments/*/payment_method

// Retrieving username that we'll need to extract name and email.
auth.ticket.get

/*
 * Opening up a database connection to our stripe database, since we want to store
 * values associating the username with stripe internal data as we proceed.
 */
data.connect:[generic|stripe]

   // Will contain customer-id and payment method reference in Stripe's systems.
   .customer-id
   data.read
      table:customers
      values
         customer_id
      where
         and
            username.eq:x:@auth.ticket.get

   // Verifying we've previously created a customer, and/or payment method
   if
      not
         exists:x:@data.read/*/*
      .lambda

         // Customer has not previously been created.
         log.error:Tried to create subscription before creating a customer or payment method
            username:x:@auth.user.get
         throw:No payment method, and/or customer created
            public:true
            status:401

   // Setting customer id and payment method.
   set-value:x:@.customer-id
      get-value:x:@data.read/*/*/customer_id

   // Checking if request had a [meta] argument.
   if
      and
         exists:x:@.arguments/*/meta
         not
            eq:x:@.arguments/*/meta
               .
         not
            eq:x:@.arguments/*/meta
               .:
      .lambda
         add:x:@data.connect/*/signal
            get-nodes:x:@.arguments/*/meta

   /*
    * Now we have a customer, a payment method, and the customer and its payment method
    * are associated with each other internally in our database, and in Stripe's systems.
    *
    * Hence, now we can create our subscription.
    */
   unwrap:x:+/*
   signal:stripe.payments.create
      amount:x:@.arguments/*/amount
      currency:x:@.arguments/*/currency
      payment_method:x:@.arguments/*/payment_method
      customer_id:x:@.customer-id

   // Creating our subscription and storing it in our database.
   data.create
      table:payments
      values
         payment:x:@signal/*/id
         username:x:@auth.ticket.get
         amount:x:@.arguments/*/amount
         currency:x:@.arguments/*/currency
         payment_method:x:@.arguments/*/payment_method
         completed:x:@signal/*/finished

   // Invoking any callback sink methods, but only if meta information was supplied.
   if
      exists:x:@.arguments/*/meta
      .lambda

         // Retrieving all slots that are interested in callback notifications.
         slots.vocabulary:payments.payments.paid.
         
         // Invoking all slots returned above.
         for-each:x:-/*
            add:x:./*/signal
               get-nodes:x:@.arguments/*/meta
               get-nodes:x:@.arguments/*/amount
               get-nodes:x:@.arguments/*/currency
            unwrap:x:+/*
            signal:x:@.dp/#
               type:payment
               username:x:@auth.ticket.get
               completed:x:@signal/@signal/*/finished

   // Checking if we've got a notifications callback, and if so, invoking it.
   config.get:"magic:stripe:notifications"
   unwrap:x:+/*/username
   try-signal:x:@config.get
      type:payment
      content:Your payment was successfully created
      username:x:@auth.ticket.get

   // Checking if charge was successful.
   if
      not
         get-value:x:@signal/*/finished
      .lambda
         unwrap:x:+/*
         return
            finished:bool:false
            payment:x:@signal/*/id
            payment_url:x:@signal/*/payment_url

   // Returning success to caller.
   unwrap:x:+/*
   return
      finished:bool:true
      payment:x:@signal/*/id
