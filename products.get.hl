
/*
 * Returns the product information from Stripe given the specified product ID.
 */
.arguments
   product:string
.description:"Returns the specified product as returned from Stripe's API"

// Sanity checking invocation.
validators.mandatory:x:@.arguments/*/product

/*
 * Applying some fairly aggressive server side cache here to avoid
 * having to invoke Stripe's API every time somebody requests product
 * information.
 */
strings.concat
   .:stripe.products.cache.
   get-value:x:@.arguments/*/product
unwrap:x:+/*/.lambda/*/.product
cache.try-get:x:@strings.concat
   expiration:6000
   .lambda

      // Forward evaluated further up.
      .product:x:@.arguments/*/product

      // Retrieving token to use for invocations towards Stripe.
      .token
      set-value:x:-
         strings.concat
            .:"Bearer "
            config.get:"magic:stripe:token"

      // Retrieving product information from Stripe.
      strings.concat
         .:"https://api.stripe.com/v1/products/"
         get-value:x:@.product
      http.get:x:-
         convert:true
         headers
            Authorization:x:@.token

      // Sanity checking above invocation.
      if
         mte
            get-value:x:@http.get
            .:int:400
         .lambda

            // Oops, something went wrong ...!!
            lambda2hyper:x:@http.post
            log.error:Something went wrong while invoking Stripe API
               result:x:@lambda2hyper
               provider:Stripe
            throw:"We could not successfully retrieve that product from Stripe's systems"
               public:true
               status:500

      // Returning Stripe information to caller.
      unwrap:x:+/*
      lambda2hyper
         name:x:@http.get/*/content/*/name
         description:x:@http.get/*/content/*/description
      return:x:-

// Making sure we apply aggressive HTTP cache control
response.headers.set
   Cache-Control: public, max-age=6000

// Returning product to caller.
hyper2lambda:x:@cache.try-get
return-nodes:x:-/*